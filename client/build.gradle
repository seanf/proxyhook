plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

dependencies {
    compile project(':common')
}

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'org.flanigan.proxyhook.client.ProxyHookClient'

// Vert.x watches for file changes in all subdirectories
// of src/ but only for files with .java extension
// NB this won't pick up changes in :common
def watchForChange = 'src/**/*.java'

// Vert.x will call this task on changes
def doOnChange = 'gradlew classes'

//noinspection GroovyAssignabilityCheck
run {
    def urls = System.getProperty('urls') ?: ''
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"] + urls.tokenize()
}

shadowJar {
    classifier = 'fat'

    manifest {
        attributes "Main-Verticle": mainVerticleName
    }

    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

